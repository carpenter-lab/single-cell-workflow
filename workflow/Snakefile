from snakemake.utils import min_version
from rules.common import WorkflowResults

min_version("3.7")


configfile: "config/config.yml"


pepfile: "pep/config.yml"


pepschema: "schema.yml"

config["pep"] = pep

module preprocessing:
    snakefile: "rules/seurat_preprocessing.smk"
    config: config

module differential_expression:
    snakefile: "rules/differential_expression.smk"
    config: config

module plotting:
    snakefile: "rules/plotting.smk"
    config: config

module clustering:
    snakefile: "rules/clustering.smk"
    config: config

use rule * from preprocessing
use rule * from clustering
use rule * from differential_expression
use rule * from plotting

rule all:
    input:
        expand("results/qc/{type}_qc_plot.png", type=["pre", "post"]),
        WorkflowResults(
            config["plotting"]["umap_plot"],
            "results/clustering/{subset}/plots/{assay}/{reduction}/{group}_split_{split}.png",
        ).create_path_list(),
        WorkflowResults(
            config["plotting"]["dot_plot"],
            "results/de/{subset}/plots/{assay}/dot_plot_by_{group}.png",
        ).create_path_list(),
        WorkflowResults(
            config["differential_expression"]["params"],
            "results/de/{subset}/{assay}/{group}_markers.{ext}",
            ext=["tsv"],
        ).create_path_list(),
